apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'

ext {
    PUBLISH_GROUP_ID = 'com.github.thejuki'
    PUBLISH_VERSION = '8.3.0'
    PUBLISH_ARTIFACT_ID = 'k-form-master'
}

apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 32
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        targetCompatibility 11
        sourceCompatibility 11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    sourceSets {
        all {
            it.java.srcDir "src/$it.name/java"
        }
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            useJUnitPlatform()
        }
    }
    lint {
        abortOnError false
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['compileDebugSources']) {

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files(mainSrc)
    classDirectories.from = files(debugTree)
    executionData.from = fileTree(dir: project.buildDir, includes: [
            "jacoco/testDebugUnitTest.exec", // Unit tests
            "outputs/code_coverage/debugAndroidTest/connected/*coverage.ec" // Android tests
    ])
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

ext {
    rendererRecyclerViewAdapterVersion = '3.0.1'
    tokenautocompleteVersion = '3.0.2'
    constraintLayoutVersion = '2.1.3'
    glideVersion = '4.13.1'
    imagepickerVersion = '2.1'
    testCoreVersion = '1.4.0'
    testExtJunitVersion = '1.1.3'
    testRulesVersion = '1.4.0'
    testExpressoVersion = '3.4.0'
    kotestVersion = '5.2.3'
    mockkVersion = '1.12.3'
    inputMaskVersion = '6.0.0'
    androidSVGVersion = '1.4'
    expandablelayoutVersion = '2.9.2'
    threetenabpVersion = '1.4.0'
    wheelpickerVersion = '1.1.3'
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Androidx
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion",
            "com.google.android.material:material:$materialVersion",
            "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion",
            "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion",
            "androidx.multidex:multidex:$multidexVersion"

    // RendererRecyclerViewAdapter
    api "com.github.vivchar:RendererRecyclerViewAdapter:$rendererRecyclerViewAdapterVersion"

    // Token Autocomplete
    api "com.splitwise:tokenautocomplete:$tokenautocompleteVersion@aar"

    // ImagePicker
    api "com.github.dhaval2404:imagepicker:$imagepickerVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // Input mask
    api "com.redmadrobot:input-mask-android:$inputMaskVersion"

    // Android SVG
    implementation "com.caverock:androidsvg-aar:$androidSVGVersion"

    // Expandable Layout (To use InlineDatePickerElement)
    implementation "net.cachapa.expandablelayout:expandablelayout:$expandablelayoutVersion"

    // ThreeTen Android Backport (To use InlineDatePickerElement)
    api "com.jakewharton.threetenabp:threetenabp:$threetenabpVersion"

    // WheelPicker (To use InlineDatePickerElement)
    implementation "cn.aigestudio.wheelpicker:WheelPicker:$wheelpickerVersion"

    // Test dependencies
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-property:$kotestVersion"
    androidTestImplementation "androidx.test:core:$testCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$testExtJunitVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testExpressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$testExpressoVersion"
}

